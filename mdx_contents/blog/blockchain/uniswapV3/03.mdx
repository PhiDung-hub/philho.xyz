---
title: UniswapV3 03 - Minting and Swapping
date: '2023-05-23T00:00:00Z'
modifiedTime: '2023-05-31T00:00:00Z'
summary: Uniswap V3 - Implement liquidity provision (minting) and DEX (token swapping).
image: 'blogs/blockchain/uniswapV3/cover.webp'
seriesName: 'Uniswap V3'
seriesIndex: 3
type: BlogPost
---

# Recap

We have discussed **Tick** and **Position** from the [previous section](https://philho.xyz/blog/blockchain/uniswapV3/02). Take notes of the following:

1. Each tick:

- is represented by `int24`.
- is managed by a `16 | 8` bit map.
- holds an `initialize` state, indicating whether the tick is active in the current position
- contains information about liquidity, fee, and time spent.

2. A position:

- is **non-fungible**.
- contains information about fee accrued for both token (within the pool).

# About Uniswap V3 core

Uniswap V3 core contracts implements critical operations such as liquidity provision, token swaps, and fee calculations.
Key contracts in the repository include:

1. [UniswapV3Pool.sol](https://github.com/Uniswap/v3-core/blob/main/contracts/UniswapV3Pool.sol): represents a specific Uniswap V3 liquidity pool and
   handles functions related to liquidity management, swapping tokens, and calculating fees.
2. [UniswapV3Factory.sol](https://github.com/Uniswap/v3-core/blob/main/contracts/UniswapV3Factory.sol): manage the overall pool creation process.
   It keeps track of all created pools and provides functions to create new pools with specific token pairs and fee tiers.
3. [UniswapV3PoolDeployer.sol](https://github.com/Uniswap/v3-core/blob/main/contracts/UniswapV3PoolDeployer.sol): an utility contract used internally by the `UniswapV3Factory` contract.
   It contains the necessary logic and functions to deploy new instances of a liquidity pool.

The `UniswapV3Factory` and `UniswapV3PoolDeployer` contracts are relatively simple and do not provide details about the protocol specification. Hence, we will not discuss them in this series.
Our main focus will be on the `UniswapV3Pool` contract, and this article will explain the processes of position minting and token swapping.

## UniswapV3 pool

The pool structure is as follows:

```solidity
contract UniswapV3Pool is IUniswapV3Pool, NoDelegateCall {
  .....
  address public immutable override factory;
  address public immutable override token0;
  address public immutable override token1;
  uint24 public immutable override fee;

  int24 public immutable override tickSpacing;

  uint128 public immutable override maxLiquidityPerTick;

  struct Slot0 {
    // the current price
    uint160 sqrtPriceX96;
    // the current tick
    int24 tick;
    // the most-recently updated index of the observations array
    uint16 observationIndex;
    // the current maximum number of observations that are being stored
    uint16 observationCardinality;
    // the next maximum number of observations to store, triggered in observations.write
    uint16 observationCardinalityNext;
    // the current protocol fee as a percentage of the swap fee taken on withdrawal
    // represented as an integer denominator (1/x)%
    uint8 feeProtocol;
    // whether the pool is locked
    bool unlocked;
  }
  Slot0 public override slot0;

  uint256 public override feeGrowthGlobal0X128;
  uint256 public override feeGrowthGlobal1X128;

  // accumulated protocol fees in token0/token1 units
  struct ProtocolFees {
    uint128 token0;
    uint128 token1;
  }
  ProtocolFees public override protocolFees;

  uint128 public override liquidity;

  mapping(int24 => Tick.Info) public override ticks;
  mapping(int16 => uint256) public override tickBitmap;
  mapping(bytes32 => Position.Info) public override positions;
  Oracle.Observation[65535] public override observations;
  .....
  /// @dev Get the pool's balance of token0
  /// @dev This function is gas optimized to avoid a redundant extcodesize check in addition to the returndatasize check
  function balance0() private view returns (uint256) {
    (bool success, bytes memory data) = token0.staticcall(abi.encodeWithSelector(IERC20Minimal.balanceOf.selector, address(this)));
    require(success && data.length >= 32);
    return abi.decode(data, (uint256));
  }

  /// @dev Get the pool's balance of token1
  /// @dev This function is gas optimized to avoid a redundant extcodesize check in addition to the returndatasize check
  function balance1() private view returns (uint256) {
    (bool success, bytes memory data) = token1.staticcall(abi.encodeWithSelector(IERC20Minimal.balanceOf.selector, address(this)));
    require(success && data.length >= 32);
    return abi.decode(data, (uint256));
  }
   /// @dev Common checks for valid tick inputs.
  function checkTicks(int24 tickLower, int24 tickUpper) private pure {
    require(tickLower < tickUpper, 'TLU');
    require(tickLower >= TickMath.MIN_TICK, 'TLM');
    require(tickUpper <= TickMath.MAX_TICK, 'TUM');
  }
  .....
}
```

A pool consists of:

- A factory instance
- A Pair of tokens (token0 and token1) and fee tier
- liquidity and fee information
- Ticks and Positions configuration as well as mappings.

On top of that, it also provides utilities like:

- Retrieve pool's balance of both tokens.
- Check for tick validity.

# Providing liquidity (position minting)

This is the portion of minting code from the **UniswapV3Pool** contract.
Where `_modifyPosition` update the position and return new position state with amount of tokens owed to _this_ pool.

```solidity
interface IUniswapV3MintCallback {
  /// @notice Called to `msg.sender` after minting liquidity to a position from IUniswapV3Pool#mint.
  /// @dev In the implementation you must pay the pool tokens owed for the minted liquidity.
  /// The caller of this method must be checked to be a UniswapV3Pool deployed by the canonical UniswapV3Factory.
  /// @param amount0Owed The amount of token0 due to the pool for the minted liquidity
  /// @param amount1Owed The amount of token1 due to the pool for the minted liquidity
  /// @param data Any data passed through by the caller via the IUniswapV3PoolActions#mint call
  function uniswapV3MintCallback(
    uint256 amount0Owed,
    uint256 amount1Owed,
    bytes calldata data
  ) external;
}

/// @inheritdoc IUniswapV3PoolActions
/// @dev noDelegateCall is applied indirectly via _modifyPosition
function mint(
  address recipient,
  int24 tickLower,
  int24 tickUpper,
  uint128 amount,
  bytes calldata data
) external override lock returns (uint256 amount0, uint256 amount1) {
  require(amount > 0);
  (, int256 amount0Int, int256 amount1Int) = _modifyPosition(
    ModifyPositionParams({owner: recipient, tickLower: tickLower, tickUpper: tickUpper, liquidityDelta: int256(amount).toInt128()})
  );

  amount0 = uint256(amount0Int);
  amount1 = uint256(amount1Int);

  uint256 balance0Before;
  uint256 balance1Before;
  if (amount0 > 0) balance0Before = balance0();
  if (amount1 > 0) balance1Before = balance1();
  IUniswapV3MintCallback(msg.sender).uniswapV3MintCallback(amount0, amount1, data);
  if (amount0 > 0) require(balance0Before.add(amount0) <= balance0(), 'M0');
  if (amount1 > 0) require(balance1Before.add(amount1) <= balance1(), 'M1');

  emit Mint(msg.sender, recipient, tickLower, tickUpper, amount, amount0, amount1);
}
```

## Minting flow

Essentially, minting contains 4 steps:

1. Register the position paramters, which are owner, price range, and liquidityDelta for the minted position.
2. Call internal `_modifyPosition` to get expected token amount given to the pool (can be negative). This will be discussed in details in later sections.
3. Call a `uniswapV3MintCallback`, the client (contract or EOA) must realize implementations, execute this callback and payback the owed amount.
   The official protocol implements this callback in **V3-Periphery** contracts at [LiquidityManagement.sol](https://github.com/Uniswap/v3-periphery/blob/main/contracts/base/LiquidityManagement.sol)
   (V3-Periphery complement the core Uniswap V3 protocol, providing features such as position management, liquidity provision, and advanced trading strategies)
4. Validate final state and emit a `Mint` event to the blockchain, indicating a liquidity povision order is placed.

## Liquidity Delta calculation

When the protocol adds liquidity to a transaction, it is necessary to calculate the amount of tokens that corresponds to the added liquidity.
This calculation is based on the formula derived in [part 1](https://philho.xyz/blog/blockchain/uniswapV3/02)

$$
\Large{
  \begin{aligned}

  \Delta X &= L \times \Delta \frac{1}{\sqrt{P}} \quad (1) \\
  \Delta Y &= L \times \Delta \sqrt{P} \quad (2)

  \end{aligned}
}
$$

```solidity
struct ModifyPositionParams {
  // the address that owns the position
  address owner;
  // the lower and upper tick of the position
  int24 tickLower;
  int24 tickUpper;
  // any change in liquidity
  int128 liquidityDelta;
}

/// @param sqrtRatioAX96 A sqrt price
/// @param sqrtRatioBX96 Another sqrt price
/// @param liquidity The amount of usable liquidity
/// @param roundUp Whether to round the amount up or down
/// @return amount0 Amount of token0 required to cover a position of size liquidity between the two passed prices
function getAmount0Delta(
  uint160 sqrtRatioAX96,
  uint160 sqrtRatioBX96,
  uint128 liquidity,
  bool roundUp
) internal pure returns (uint256 amount0) {
  if (sqrtRatioAX96 > sqrtRatioBX96) (sqrtRatioAX96, sqrtRatioBX96) = (sqrtRatioBX96, sqrtRatioAX96);

  uint256 numerator1 = uint256(liquidity) << FixedPoint96.RESOLUTION;
  uint256 numerator2 = sqrtRatioBX96 - sqrtRatioAX96;

  require(sqrtRatioAX96 > 0); // price must not be 0

  return
    roundUp
      ? UnsafeMath.divRoundingUp(FullMath.mulDivRoundingUp(numerator1, numerator2, sqrtRatioBX96), sqrtRatioAX96)
      : FullMath.mulDiv(numerator1, numerator2, sqrtRatioBX96) / sqrtRatioAX96;
}

/// @param sqrtRatioAX96 A sqrt price
/// @param sqrtRatioBX96 Another sqrt price
/// @param liquidity The amount of usable liquidity
/// @param roundUp Whether to round the amount up or down
/// @return amount1 Amount of token1 required to cover a position of size liquidity between the two passed prices
function getAmount1Delta(
  uint160 sqrtRatioAX96,
  uint160 sqrtRatioBX96,
  uint128 liquidity,
  bool roundUp
) internal pure returns (uint256 amount1) {
  if (sqrtRatioAX96 > sqrtRatioBX96) (sqrtRatioAX96, sqrtRatioBX96) = (sqrtRatioBX96, sqrtRatioAX96);

  return
    roundUp
      ? FullMath.mulDivRoundingUp(liquidity, sqrtRatioBX96 - sqrtRatioAX96, FixedPoint96.Q96)
      : FullMath.mulDiv(liquidity, sqrtRatioBX96 - sqrtRatioAX96, FixedPoint96.Q96);
}
```

From the code:

- `getAmount0Delta` calculate $\Delta X$, as per formula (1) stated above.
- `getAmount1Delta` calculate $\Delta Y$, as per formula (2) stated above.

## Position minting - \_updatePosition

This `_updatePosition` function update the position state given an amount of liquidity change (`liquidityDelta`). It:

1. Call `Tick.update(...)` (refer to [part 2 of this series](https://philho.xyz/blog/blockchain/uniswapV3/02#Tick-functions)) on the storage variable `ticks`, which consists of global ticks' state.
2. Update tick bitmap, flipped state, and fee.
3. Call `Position.update(...)` (refer to [part 2 of this series](https://philho.xyz/blog/blockchain/uniswapV3/02#Position)).

```solidity
/// @dev Gets and updates a position with the given liquidity delta
/// @param owner the owner of the position
/// @param tickLower the lower tick of the position's tick range
/// @param tickUpper the upper tick of the position's tick range
/// @param tick the current tick, passed to avoid sloads
function _updatePosition(
  address owner,
  int24 tickLower,
  int24 tickUpper,
  int128 liquidityDelta,
  int24 tick
) private returns (Position.Info storage position) {
  position = positions.get(owner, tickLower, tickUpper);

  uint256 _feeGrowthGlobal0X128 = feeGrowthGlobal0X128; // SLOAD for gas optimization
  uint256 _feeGrowthGlobal1X128 = feeGrowthGlobal1X128; // SLOAD for gas optimization

  // if we need to update the ticks, do it
  bool flippedLower;
  bool flippedUpper;
  if (liquidityDelta != 0) {
    uint32 time = _blockTimestamp();
    (int56 tickCumulative, uint160 secondsPerLiquidityCumulativeX128) = observations.observeSingle(
      time,
      0,
      slot0.tick,
      slot0.observationIndex,
      liquidity,
      slot0.observationCardinality
    );

    flippedLower = ticks.update(
      tickLower,
      tick,
      liquidityDelta,
      _feeGrowthGlobal0X128,
      _feeGrowthGlobal1X128,
      secondsPerLiquidityCumulativeX128,
      tickCumulative,
      time,
      false,
      maxLiquidityPerTick
    );
    flippedUpper = ticks.update(
      tickUpper,
      tick,
      liquidityDelta,
      _feeGrowthGlobal0X128,
      _feeGrowthGlobal1X128,
      secondsPerLiquidityCumulativeX128,
      tickCumulative,
      time,
      true,
      maxLiquidityPerTick
    );

    if (flippedLower) {
      tickBitmap.flipTick(tickLower, tickSpacing);
    }
    if (flippedUpper) {
      tickBitmap.flipTick(tickUpper, tickSpacing);
    }
  }

  (uint256 feeGrowthInside0X128, uint256 feeGrowthInside1X128) = ticks.getFeeGrowthInside(
    tickLower,
    tickUpper,
    tick,
    _feeGrowthGlobal0X128,
    _feeGrowthGlobal1X128
  );

  position.update(liquidityDelta, feeGrowthInside0X128, feeGrowthInside1X128);

  // clear any tick data that is no longer needed
  if (liquidityDelta < 0) {
    if (flippedLower) {
      ticks.clear(tickLower);
    }
    if (flippedUpper) {
      ticks.clear(tickUpper);
    }
  }
}
```

## Wrap it up - \_modifyPosition

We can finish the final piece of minting logic - `_modifyPosition` - which essentially:

1. Perform parameters checks.
2. Call for `_updatePosition`.
3. Given the liquidity delta from `_updatePosition`, calculate tokens owed to protocols for 3 cases:

- If $currentTick < lowerTick$, all are **token 0** [price OUTSIDE (below) range].
- If $lowerTick \leq currentTick \leq upperTick$, mix of **token 0** and **token 1** [price INSIDE range].
  For this special case, also update state of external oracle (will be discussed in part 5.
- If $currentTick > upperTick$, all are **token 1** [price OUTSIDE (above) range].

```solidity
/// @dev Common checks for valid tick inputs.
function checkTicks(int24 tickLower, int24 tickUpper) private pure {
  require(tickLower < tickUpper, 'TLU');
  require(tickLower >= TickMath.MIN_TICK, 'TLM');
  require(tickUpper <= TickMath.MAX_TICK, 'TUM');
}

/// @param params the position details and the change to the position's liquidity to effect
/// @return position a storage pointer referencing the position with the given owner and tick range
/// @return amount0 the amount of token0 owed to the pool, negative if the pool should pay the recipient
/// @return amount1 the amount of token1 owed to the pool, negative if the pool should pay the recipient
function _modifyPosition(ModifyPositionParams memory params)
  private
  noDelegateCall
  returns (
    Position.Info storage position,
    int256 amount0,
    int256 amount1
  )
{
  checkTicks(params.tickLower, params.tickUpper);

  Slot0 memory _slot0 = slot0; // SLOAD for gas optimization

  position = _updatePosition(params.owner, params.tickLower, params.tickUpper, params.liquidityDelta, _slot0.tick);

  if (params.liquidityDelta != 0) {
    if (_slot0.tick < params.tickLower) {
      // current tick is below the passed range; liquidity can only become in range by crossing from left to
      // right, when we'll need _more_ token0 (it's becoming more valuable) so user must provide it
      amount0 = SqrtPriceMath.getAmount0Delta(
        TickMath.getSqrtRatioAtTick(params.tickLower),
        TickMath.getSqrtRatioAtTick(params.tickUpper),
        params.liquidityDelta
      );
    } else if (_slot0.tick < params.tickUpper) {
      // current tick is inside the passed range
      uint128 liquidityBefore = liquidity; // SLOAD for gas optimization

      // write an oracle entry
      (slot0.observationIndex, slot0.observationCardinality) = observations.write(
        _slot0.observationIndex,
        _blockTimestamp(),
        _slot0.tick,
        liquidityBefore,
        _slot0.observationCardinality,
        _slot0.observationCardinalityNext
      );

      amount0 = SqrtPriceMath.getAmount0Delta(
        _slot0.sqrtPriceX96,
        TickMath.getSqrtRatioAtTick(params.tickUpper),
        params.liquidityDelta
      );
      amount1 = SqrtPriceMath.getAmount1Delta(
        TickMath.getSqrtRatioAtTick(params.tickLower),
        _slot0.sqrtPriceX96,
        params.liquidityDelta
      );

      liquidity = LiquidityMath.addDelta(liquidityBefore, params.liquidityDelta);
    } else {
      // current tick is above the passed range; liquidity can only become in range by crossing from right to
      // left, when we'll need _more_ token1 (it's becoming more valuable) so user must provide it
      amount1 = SqrtPriceMath.getAmount1Delta(
        TickMath.getSqrtRatioAtTick(params.tickLower),
        TickMath.getSqrtRatioAtTick(params.tickUpper),
        params.liquidityDelta
      );
    }
  }
}
```

# Token Swapping

Some utilities structs:

```solidity
// Contains cached information while performing steps of a swap
struct SwapCache {
  // the protocol fee for the input token
  uint8 feeProtocol;
  // liquidity at the beginning of the swap
  uint128 liquidityStart;
  // the timestamp of the current block
  uint32 blockTimestamp;
  // the current value of the tick accumulator, computed only if we cross an initialized tick
  int56 tickCumulative;
  // the current value of seconds per liquidity accumulator, computed only if we cross an initialized tick
  uint160 secondsPerLiquidityCumulativeX128;
  // whether we've computed and cached the above two accumulators
  bool computedLatestObservation;
}

// The top level state of the swap, the results of which are recorded in storage at the end
struct SwapState {
  // the amount remaining to be swapped in/out of the input/output asset
  int256 amountSpecifiedRemaining;
  // the amount already swapped out/in of the output/input asset
  int256 amountCalculated;
  // current sqrt(price)
  uint160 sqrtPriceX96;
  // the tick associated with the current price
  int24 tick;
  // the global fee growth of the input token
  uint256 feeGrowthGlobalX128;
  // amount of input token paid as protocol fee
  uint128 protocolFee;
  // the current liquidity in range
  uint128 liquidity;
}

// Information about each step in swapping token
struct StepComputations {
  // the price at the beginning of the step
  uint160 sqrtPriceStartX96;
  // the next tick to swap to from the current tick in the swap direction
  int24 tickNext;
  // whether tickNext is initialized or not
  bool initialized;
  // sqrt(price) for the next tick (1/0)
  uint160 sqrtPriceNextX96;
  // how much is being swapped in in this step
  uint256 amountIn;
  // how much is being swapped out
  uint256 amountOut;
  // how much fee is being paid in
  uint256 feeAmount;
}
```

The implementation of token swapping involves the following key steps:

1. Input validation
2. Initialize `SwapCache`, `SwapState`, and `Slot0`
3. Perform swapping logic until `amountSpecified` is drained out, i.e. the swap is completed. consist of the following steps:
   - Initialize `StepComputations`, get state of the next tick (a)
   - Perform a swap step, update amount left, protocol fee, and price movement (slippage) if any. Note that swaps can be performed in reverse direction (`amountSpecified` < 0)
     and a swap step can only exhausted the liquidity in current tick.
   - Update tick state based on new price calculated (as well as liquidity if there is any liquidityNet).
4. Update global price, ticks' state, liquidity, fee, and token amount (after the swap).
5. Perform token transfer, token repayment (to the pool), and emit a `Swap` event.

```solidity
function swap(
  address recipient,
  bool zeroForOne,
  int256 amountSpecified,
  uint160 sqrtPriceLimitX96,
  bytes calldata data
) external override noDelegateCall returns (int256 amount0, int256 amount1) {
  /// START STEP 1: Input validation
  require(amountSpecified != 0, 'AS');

  Slot0 memory slot0Start = slot0;

  require(slot0Start.unlocked, 'LOK');
  require(
    zeroForOne
      ? sqrtPriceLimitX96 < slot0Start.sqrtPriceX96 && sqrtPriceLimitX96 > TickMath.MIN_SQRT_RATIO
      : sqrtPriceLimitX96 > slot0Start.sqrtPriceX96 && sqrtPriceLimitX96 < TickMath.MAX_SQRT_RATIO,
    'SPL'
  );
  /// END STEP 1

  slot0.unlocked = false;

  /// START STEP 2: Initialization
  SwapCache memory cache = SwapCache({
    liquidityStart: liquidity,
    blockTimestamp: _blockTimestamp(),
    feeProtocol: zeroForOne ? (slot0Start.feeProtocol % 16) : (slot0Start.feeProtocol >> 4),
    secondsPerLiquidityCumulativeX128: 0,
    tickCumulative: 0,
    computedLatestObservation: false
  });

  bool exactInput = amountSpecified > 0;

  SwapState memory state = SwapState({
    amountSpecifiedRemaining: amountSpecified,
    amountCalculated: 0,
    sqrtPriceX96: slot0Start.sqrtPriceX96,
    tick: slot0Start.tick,
    feeGrowthGlobalX128: zeroForOne ? feeGrowthGlobal0X128 : feeGrowthGlobal1X128,
    protocolFee: 0,
    liquidity: cache.liquidityStart
  });
  /// END STEP 2

  /// START STEP 3: swap until amountSpecified is drained
  // continue swapping as long as we haven't used the entire input/output and haven't reached the price limit
  while (state.amountSpecifiedRemaining != 0 && state.sqrtPriceX96 != sqrtPriceLimitX96) {
    /// START STEP 3.a
    StepComputations memory step;

    step.sqrtPriceStartX96 = state.sqrtPriceX96;

    (step.tickNext, step.initialized) = tickBitmap.nextInitializedTickWithinOneWord(
      state.tick,
      tickSpacing,
      zeroForOne
    );

    // ensure that we do not overshoot the min/max tick, as the tick bitmap is not aware of these bounds
    if (step.tickNext < TickMath.MIN_TICK) {
      step.tickNext = TickMath.MIN_TICK;
    } else if (step.tickNext > TickMath.MAX_TICK) {
      step.tickNext = TickMath.MAX_TICK;
    }

    // get the price for the next tick
    step.sqrtPriceNextX96 = TickMath.getSqrtRatioAtTick(step.tickNext);
    /// END STEP 3.a

    /// START STEP 3.b
    // compute values to swap to the target tick, price limit, or point where input/output amount is exhausted
    (state.sqrtPriceX96, step.amountIn, step.amountOut, step.feeAmount) = SwapMath.computeSwapStep(
      state.sqrtPriceX96,
      (zeroForOne ? step.sqrtPriceNextX96 < sqrtPriceLimitX96 : step.sqrtPriceNextX96 > sqrtPriceLimitX96)
        ? sqrtPriceLimitX96
        : step.sqrtPriceNextX96,
      state.liquidity,
      state.amountSpecifiedRemaining,
      fee
    );

    if (exactInput) {
      state.amountSpecifiedRemaining -= (step.amountIn + step.feeAmount).toInt256();
      state.amountCalculated = state.amountCalculated.sub(step.amountOut.toInt256());
    } else {
      state.amountSpecifiedRemaining += step.amountOut.toInt256();
      state.amountCalculated = state.amountCalculated.add((step.amountIn + step.feeAmount).toInt256());
    }

    // if the protocol fee is on, calculate how much is owed, decrement feeAmount, and increment protocolFee
    if (cache.feeProtocol > 0) {
      uint256 delta = step.feeAmount / cache.feeProtocol;
      step.feeAmount -= delta;
      state.protocolFee += uint128(delta);
    }

    // update global fee tracker
    if (state.liquidity > 0)
      state.feeGrowthGlobalX128 += FullMath.mulDiv(step.feeAmount, FixedPoint128.Q128, state.liquidity);
    /// END STEP 3.b

    /// START STEP 3.c
    // shift tick if we reached the next price
    if (state.sqrtPriceX96 == step.sqrtPriceNextX96) {
      // if the tick is initialized, run the tick transition
      if (step.initialized) {
        // check for the placeholder value, which we replace with the actual value the first time the swap
        // crosses an initialized tick
        if (!cache.computedLatestObservation) {
          (cache.tickCumulative, cache.secondsPerLiquidityCumulativeX128) = observations.observeSingle(
            cache.blockTimestamp,
            0,
            slot0Start.tick,
            slot0Start.observationIndex,
            cache.liquidityStart,
            slot0Start.observationCardinality
          );
          cache.computedLatestObservation = true;
        }
        int128 liquidityNet = ticks.cross(
          step.tickNext,
          (zeroForOne ? state.feeGrowthGlobalX128 : feeGrowthGlobal0X128),
          (zeroForOne ? feeGrowthGlobal1X128 : state.feeGrowthGlobalX128),
          cache.secondsPerLiquidityCumulativeX128,
          cache.tickCumulative,
          cache.blockTimestamp
        );
        // if we're moving leftward, we interpret liquidityNet as the opposite sign
        // safe because liquidityNet cannot be type(int128).min
        if (zeroForOne) liquidityNet = -liquidityNet;

        state.liquidity = LiquidityMath.addDelta(state.liquidity, liquidityNet);
      }

      state.tick = zeroForOne ? step.tickNext - 1 : step.tickNext;
    } else if (state.sqrtPriceX96 != step.sqrtPriceStartX96) {
      // recompute unless we're on a lower tick boundary (i.e. already transitioned ticks), and haven't moved
      state.tick = TickMath.getTickAtSqrtRatio(state.sqrtPriceX96);
    }
    /// END STEP 3.c
  }
  /// END STEP 3

  /// START STEP 4: Update global states after the swap
  // update tick and write an oracle entry if the tick change
  if (state.tick != slot0Start.tick) {
    (uint16 observationIndex, uint16 observationCardinality) = observations.write(
      slot0Start.observationIndex,
      cache.blockTimestamp,
      slot0Start.tick,
      cache.liquidityStart,
      slot0Start.observationCardinality,
      slot0Start.observationCardinalityNext
    );
    (slot0.sqrtPriceX96, slot0.tick, slot0.observationIndex, slot0.observationCardinality) = (
      state.sqrtPriceX96,
      state.tick,
      observationIndex,
      observationCardinality
    );
  } else {
    // otherwise just update the price
    slot0.sqrtPriceX96 = state.sqrtPriceX96;
  }

  // update liquidity if it changed
  if (cache.liquidityStart != state.liquidity) liquidity = state.liquidity;

  // update fee growth global and, if necessary, protocol fees
  // overflow is acceptable, protocol has to withdraw before it hits type(uint128).max fees
  if (zeroForOne) {
    feeGrowthGlobal0X128 = state.feeGrowthGlobalX128;
    if (state.protocolFee > 0) protocolFees.token0 += state.protocolFee;
  } else {
    feeGrowthGlobal1X128 = state.feeGrowthGlobalX128;
    if (state.protocolFee > 0) protocolFees.token1 += state.protocolFee;
  }

  (amount0, amount1) = zeroForOne == exactInput
    ? (amountSpecified - state.amountSpecifiedRemaining, state.amountCalculated)
    : (state.amountCalculated, amountSpecified - state.amountSpecifiedRemaining);
  /// END STEP 4


  /// START STEP 5: Transfer, payment and emit swap event.
  // do the transfers and collect payment
  if (zeroForOne) {
    if (amount1 < 0) TransferHelper.safeTransfer(token1, recipient, uint256(-amount1));

    uint256 balance0Before = balance0();
    IUniswapV3SwapCallback(msg.sender).uniswapV3SwapCallback(amount0, amount1, data);
    require(balance0Before.add(uint256(amount0)) <= balance0(), 'IIA');
  } else {
    if (amount0 < 0) TransferHelper.safeTransfer(token0, recipient, uint256(-amount0));

    uint256 balance1Before = balance1();
    IUniswapV3SwapCallback(msg.sender).uniswapV3SwapCallback(amount0, amount1, data);
    require(balance1Before.add(uint256(amount1)) <= balance1(), 'IIA');
  }

  emit Swap(msg.sender, recipient, amount0, amount1, state.sqrtPriceX96, state.liquidity, state.tick);
  slot0.unlocked = true;
  /// END STEP 5
}
```

## Flash

Aside from swap, UniswapV3 also supports flash loan. This allows users to:

1. Borrow (potentially unlimited) amount of tokens.
2. Perform arbitrary action in between transactions, e.g. use the borrowed tokens to trade on other DEX.
3. Repay the borrowed amount plus fee of the flash loan.

Historically, flash loan was a default feature of UniswapV2. In V2, any swap can potentially be a flash swap as there is no need (or no way) to differentiate between them.
V2 swap implmentation can be referenced [here](https://github.com/Uniswap/v2-core/blob/master/contracts/UniswapV2Pair.sol#L159).
V2 swap fee is fixed and set at 3/1000, i.e $L_{new} \times 997 >= L_{old} \times 1000$

```solidity
function flash(
  address recipient,
  uint256 amount0,
  uint256 amount1,
  bytes calldata data
) external override lock noDelegateCall {
  uint128 _liquidity = liquidity;
  require(_liquidity > 0, 'L');

  uint256 fee0 = FullMath.mulDivRoundingUp(amount0, fee, 1e6);
  uint256 fee1 = FullMath.mulDivRoundingUp(amount1, fee, 1e6);
  uint256 balance0Before = balance0();
  uint256 balance1Before = balance1();

  if (amount0 > 0) TransferHelper.safeTransfer(token0, recipient, amount0);
  if (amount1 > 0) TransferHelper.safeTransfer(token1, recipient, amount1);

  IUniswapV3FlashCallback(msg.sender).uniswapV3FlashCallback(fee0, fee1, data);

  uint256 balance0After = balance0();
  uint256 balance1After = balance1();

  require(balance0Before.add(fee0) <= balance0After, 'F0');
  require(balance1Before.add(fee1) <= balance1After, 'F1');

  // sub is safe because we know balanceAfter is gt balanceBefore by at least fee
  uint256 paid0 = balance0After - balance0Before;
  uint256 paid1 = balance1After - balance1Before;

  if (paid0 > 0) {
    uint8 feeProtocol0 = slot0.feeProtocol % 16;
    uint256 fees0 = feeProtocol0 == 0 ? 0 : paid0 / feeProtocol0;
    if (uint128(fees0) > 0) protocolFees.token0 += uint128(fees0);
    feeGrowthGlobal0X128 += FullMath.mulDiv(paid0 - fees0, FixedPoint128.Q128, _liquidity);
  }
  if (paid1 > 0) {
    uint8 feeProtocol1 = slot0.feeProtocol >> 4;
    uint256 fees1 = feeProtocol1 == 0 ? 0 : paid1 / feeProtocol1;
    if (uint128(fees1) > 0) protocolFees.token1 += uint128(fees1);
    feeGrowthGlobal1X128 += FullMath.mulDiv(paid1 - fees1, FixedPoint128.Q128, _liquidity);
  }

  emit Flash(msg.sender, recipient, amount0, amount1, paid0, paid1);
}
```

# What's next

Next time in this series, we will reconstruct a minimal demo of Uniswap V3.
I hope you all will be gainin hands-on experience with the protocol.

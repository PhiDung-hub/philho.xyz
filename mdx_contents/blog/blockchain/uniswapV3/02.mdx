---
title: UniswapV3 02 - Tick and Position
date: '2023-05-15T00:00:00Z'
modifiedTime: '2023-05-23T00:00:00Z'
summary: Uniswap V3 - Explore tick and position structure in concentrated liquidity.
image: 'blogs/blockchain/uniswapV3/cover.webp'
type: BlogPost
---

# Recap

These concepts has been discussed in [previous part](https://philho.xyz/blog/blockchain/uniswapV3/01)

$$
\Large{
\begin{aligned}

  L &= \sqrt{X \times Y} \quad (1) \\

  \Delta Y &= L \times \Delta \sqrt{P} \quad (2) \\

  \Delta X &= L \times \Delta \frac{1}{\sqrt{P}} \quad (3) \\

  p(i) &= 1.0001^i \quad (4)

\end{aligned}
}
$$

We will now proceed with the implementation of the concentrated liquidity mechanism.
This process relies on tick math and fixed point math algorithms,
which can be explored in detail through the [library implementations](https://github.com/Uniswap/v3-core/blob/main/contracts/libraries) available in the official repositories.
For our current discussion, we will be focusing on the following aspects:

- [Position](https://github.com/Uniswap/v3-core/blob/main/contracts/libraries/Position.sol) - Represent an address' liquidity within a tick range (hence concentrated), also tracking fees earned
- [Tick](https://github.com/Uniswap/v3-core/blob/main/contracts/libraries/Tick.sol) - Manage tick liquidity and fee acculmulation (within _this_ tick)
- [SwapMath](https://github.com/Uniswap/v3-core/blob/main/contracts/libraries/SwapMath.sol) - Compute result of a swap (or trade)

For a high level overview, refer to the [SECTION 6 - Implementing Concentrated Liquidity](https://uniswap.org/whitepaper-v3.pdf) in the official white-paper.

# Tick

Before looking into the actual implementation of ticks, let's see how the tick is represented and stored within solidity

## Tick Bitmap

Tick bitmaps are responsible for packing ticks' initialized state (whether active or not) using:

- **16 bits key** - `wordPosition = int16(tick >> 8)`
- **8 bits value** - position of the tick within that word, `bitPos = uint8(tick % 256)`

_**Note:** a tick is represented as an **int24** since they are bounded by [-887272, 887272] (discussed in [part 1](https://philho.xyz/blog/blockchain/uniswapV3/01#2.-Tick%20system) of this series)._

### Why 16 | 8 bit map?

Firstly, we need an efficient data structure to store ticks' state for access.
Obviously, it's no good using all 887272 booleans storage variable for each position initialized as this will incur astronomical gas on Ethereum (as of current time).
Therefore the strategy is using a simple (lazy) storage that:

- Is a 16 (bits key) | 8 (bits value) bit map, each word is 256 bits since it's Ethereum standard.
- An entry (or word) is accessed only if active, i.e. containing at least 1 active tick. $\Rightarrow$ very high utilization rate for most positions, since concentrated liquidity means tick only span within a contiguous range.
- Each tick only takes 1 bits (instead of 1 byte if using Solidity boolean) $\Rightarrow$ 8 times saving.

### How about the math?

The source code for [TickBitmap.sol](https://github.com/Uniswap/v3-core/blob/main/contracts/libraries/TickBitmap.sol)

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity >=0.5.0;

import './BitMath.sol';

/// @title Packed tick initialized state library
/// @notice Stores a packed mapping of tick index to its initialized state
/// @dev The mapping uses int16 for keys since ticks are represented as int24 and there are 256 (2^8) values per word.
library TickBitmap {
    /// @notice Computes the position in the mapping where the initialized bit for a tick lives
    /// @param tick The tick for which to compute the position
    /// @return wordPos The key in the mapping containing the word in which the bit is stored
    /// @return bitPos The bit position in the word where the flag is stored
    function position(int24 tick) private pure returns (int16 wordPos, uint8 bitPos) {
        wordPos = int16(tick >> 8);
        bitPos = uint8(tick % 256);
    }

    /// @notice Flips the initialized state for a given tick from false to true, or vice versa
    /// @param self The mapping in which to flip the tick
    /// @param tick The tick to flip
    /// @param tickSpacing The spacing between usable ticks
    function flipTick(
        mapping(int16 => uint256) storage self,
        int24 tick,
        int24 tickSpacing
    ) internal {
        require(tick % tickSpacing == 0); // ensure that the tick is spaced
        (int16 wordPos, uint8 bitPos) = position(tick / tickSpacing);
        uint256 mask = 1 << bitPos;
        self[wordPos] ^= mask;
    }

    /// @notice Returns the next initialized tick contained in the same word (or adjacent word) as the tick that is either
    /// to the left (less than or equal to) or right (greater than) of the given tick
    /// @param self The mapping in which to compute the next initialized tick
    /// @param tick The starting tick
    /// @param tickSpacing The spacing between usable ticks
    /// @param lte Whether to search for the next initialized tick to the left (less than or equal to the starting tick)
    /// @return next The next initialized or uninitialized tick up to 256 ticks away from the current tick
    /// @return initialized Whether the next tick is initialized, as the function only searches within up to 256 ticks
    function nextInitializedTickWithinOneWord(
        mapping(int16 => uint256) storage self,
        int24 tick,
        int24 tickSpacing,
        bool lte
    ) internal view returns (int24 next, bool initialized) {
        int24 compressed = tick / tickSpacing;
        if (tick < 0 && tick % tickSpacing != 0) compressed--; // round towards negative infinity

        if (lte) {
            (int16 wordPos, uint8 bitPos) = position(compressed);
            // all the 1s at or to the right of the current bitPos
            uint256 mask = (1 << bitPos) - 1 + (1 << bitPos);
            uint256 masked = self[wordPos] & mask;

            // if there are no initialized ticks to the right of or at the current tick, return rightmost in the word
            initialized = masked != 0;
            // overflow/underflow is possible, but prevented externally by limiting both tickSpacing and tick
            next = initialized
                ? (compressed - int24(bitPos - BitMath.mostSignificantBit(masked))) * tickSpacing
                : (compressed - int24(bitPos)) * tickSpacing;
        } else {
            // start from the word of the next tick, since the current tick state doesn't matter
            (int16 wordPos, uint8 bitPos) = position(compressed + 1);
            // all the 1s at or to the left of the bitPos
            uint256 mask = ~((1 << bitPos) - 1);
            uint256 masked = self[wordPos] & mask;

            // if there are no initialized ticks to the left of the current tick, return leftmost in the word
            initialized = masked != 0;
            // overflow/underflow is possible, but prevented externally by limiting both tickSpacing and tick
            next = initialized
                ? (compressed + 1 + int24(BitMath.leastSignificantBit(masked) - bitPos)) * tickSpacing
                : (compressed + 1 + int24(type(uint8).max - bitPos)) * tickSpacing;
        }
    }
}
```

A few notes:

- `Bitmap.sol` using an $O(log_2 n)$ algorithm to find the Most/Least Significant Bit of a number $n$.
- function `flipTick(...)` require a `tickSpacing` input, which specify steps between usable tick. This is useful for several occasions mentioned below.
- function `nextInitializedTickWithinOneWord(...)` tends to round up toward negative infinity ticks. This means the $i^{th}$ tick is responsible for
  $tickPrice \in [i \times tickSpacing, (i+1) \times tickSpacing)$, which is expected.

<Quote author="Uniswap V3 white-paper">

"...UNI governance also has the ability to add additional fee tiers.
When it adds a new fee tier, it can also define the tickSpacing
(see Section 6.1) corresponding to that fee tier. Once a fee tier is
added to the factory, it cannot be removed (and the tickSpacing
cannot be changed). The initial fee tiers and tick spacings supported
are 0.05% (with a tick spacing of 10, approximately 0.10% between
initializable ticks), 0.30% (with a tick spacing of 60, approximately
0.60% between initializable ticks), and 1% (with a tick spacing of
200, approximately 2.02% between ticks...

// Section 6.1

Not every tick can be initialized. The pool is instantiated with a
parameter, tickSpacing (ùë°ùë† ); only ticks with indexes that are divisible by tickSpacing can be initialized. For example, if tickSpacing
is 2, then only even ticks (...-4, -2, 0, 2, 4...) can be initialized. Small
choices for tickSpacing allow tighter and more precise ranges, but
may cause swaps to be more gas-intensive (since each initialized
tick that a swap crosses imposes a gas cost on the swapper)."

</Quote>

## Tick math

Tick math is wielded by black magic [here](https://github.com/Uniswap/v3-core/blob/v1.0.0/contracts/libraries/TickMath.sol), which containing:

- Tick and corresponding $\sqrt P$ boundary ($P$ is price).
- Effective conversion of $\sqrt P \leftrightarrow tick$ (Using pre-calculated values so conversion can be done in $O(log_2{n})$ times).

## Tick structure and mechanism

Tick implementations details is referenced from the source code - [Tick.sol](https://github.com/Uniswap/v3-core/blob/v1.0.0/contracts/libraries/Tick.sol)

### Tick layout

From the source code:

```solidity
struct Info {
  // the total position liquidity that references this tick
  uint128 liquidityGross;
  // amount of net liquidity added (subtracted) when tick is crossed from left to right (right to left),
  int128 liquidityNet;
  // fee growth per unit of liquidity on the _other_ side of this tick (relative to the current tick)
  // only has relative meaning, not absolute ‚Äî the value depends on when the tick is initialized
  uint256 feeGrowthOutside0X128;
  uint256 feeGrowthOutside1X128;
  // the cumulative tick value on the other side of the tick
  int56 tickCumulativeOutside;
  // the seconds per unit of liquidity on the _other_ side of this tick (relative to the current tick)
  // only has relative meaning, not absolute ‚Äî the value depends on when the tick is initialized
  uint160 secondsPerLiquidityOutsideX128;
  // the seconds spent on the other side of the tick (relative to the current tick)
  // only has relative meaning, not absolute ‚Äî the value depends on when the tick is initialized
  uint32 secondsOutside;
  // true iff the tick is initialized, i.e. the value is exactly equivalent to the expression liquidityGross != 0
  // these 8 bits are set to prevent fresh sstores when crossing newly initialized ticks
  bool initialized;
}
```

A tick containing info about

- liquidity grossed within this tick - `liquidityGross`, and net liquidity movement when tick is crossed from either side - `liquidityNet`.
- initialized state indicating whether this tick is active - `initialized`.
- `tickCumulativeOutside`, `secondsOutside`, `secondsPerLiquidityOutsideX128` keep track of accumulated within a selective range for each tick.
  These values are not used within the contract, but are tracked for the benefit of external contracts that need
  more finegrained information about the pool‚Äôs behavior, e.g. for purposes like liquidity mining.
  For example, this is useful for Time Weighted Average (TWAP) Oracle implementations.
  Their update schemes will be discussed in next part.

### Tick functions

From the source code:

```solidity
/// @notice Derives max liquidity per tick from given tick spacing
/// @dev Executed within the pool constructor
/// @param tickSpacing The amount of required tick separation, realized in multiples of `tickSpacing`
///     e.g., a tickSpacing of 3 requires ticks to be initialized every 3rd tick i.e., ..., -6, -3, 0, 3, 6, ...
/// @return The max liquidity per tick
function tickSpacingToMaxLiquidityPerTick(int24 tickSpacing) internal pure returns (uint128) {
    int24 minTick = (TickMath.MIN_TICK / tickSpacing) * tickSpacing;
    int24 maxTick = (TickMath.MAX_TICK / tickSpacing) * tickSpacing;
    uint24 numTicks = uint24((maxTick - minTick) / tickSpacing) + 1;
    return type(uint128).max / numTicks;
}

/// @notice Retrieves fee growth data
/// @param self The mapping containing all tick information for initialized ticks
/// @param tickLower The lower tick boundary of the position
/// @param tickUpper The upper tick boundary of the position
/// @param tickCurrent The current tick
/// @param feeGrowthGlobal0X128 The all-time global fee growth, per unit of liquidity, in token0
/// @param feeGrowthGlobal1X128 The all-time global fee growth, per unit of liquidity, in token1
/// @return feeGrowthInside0X128 The all-time fee growth in token0, per unit of liquidity, inside the position's tick boundaries
/// @return feeGrowthInside1X128 The all-time fee growth in token1, per unit of liquidity, inside the position's tick boundaries
function getFeeGrowthInside(
    mapping(int24 => Tick.Info) storage self,
    int24 tickLower,
    int24 tickUpper,
    int24 tickCurrent,
    uint256 feeGrowthGlobal0X128,
    uint256 feeGrowthGlobal1X128
) internal view returns (uint256 feeGrowthInside0X128, uint256 feeGrowthInside1X128) {
    Info storage lower = self[tickLower];
    Info storage upper = self[tickUpper];

    // calculate fee growth below
    uint256 feeGrowthBelow0X128;
    uint256 feeGrowthBelow1X128;
    if (tickCurrent >= tickLower) {
        feeGrowthBelow0X128 = lower.feeGrowthOutside0X128;
        feeGrowthBelow1X128 = lower.feeGrowthOutside1X128;
    } else {
        feeGrowthBelow0X128 = feeGrowthGlobal0X128 - lower.feeGrowthOutside0X128;
        feeGrowthBelow1X128 = feeGrowthGlobal1X128 - lower.feeGrowthOutside1X128;
    }

    // calculate fee growth above
    uint256 feeGrowthAbove0X128;
    uint256 feeGrowthAbove1X128;
    if (tickCurrent < tickUpper) {
        feeGrowthAbove0X128 = upper.feeGrowthOutside0X128;
        feeGrowthAbove1X128 = upper.feeGrowthOutside1X128;
    } else {
        feeGrowthAbove0X128 = feeGrowthGlobal0X128 - upper.feeGrowthOutside0X128;
        feeGrowthAbove1X128 = feeGrowthGlobal1X128 - upper.feeGrowthOutside1X128;
    }

    feeGrowthInside0X128 = feeGrowthGlobal0X128 - feeGrowthBelow0X128 - feeGrowthAbove0X128;
    feeGrowthInside1X128 = feeGrowthGlobal1X128 - feeGrowthBelow1X128 - feeGrowthAbove1X128;
}

/// @notice Updates a tick and returns true if the tick was flipped from initialized to uninitialized, or vice versa
/// @param self The mapping containing all tick information for initialized ticks
/// @param tick The tick that will be updated
/// @param tickCurrent The current tick
/// @param liquidityDelta A new amount of liquidity to be added (subtracted) when tick is crossed from left to right (right to left)
/// @param feeGrowthGlobal0X128 The all-time global fee growth, per unit of liquidity, in token0
/// @param feeGrowthGlobal1X128 The all-time global fee growth, per unit of liquidity, in token1
/// @param secondsPerLiquidityCumulativeX128 The all-time seconds per max(1, liquidity) of the pool
/// @param tickCumulative The tick * time elapsed since the pool was first initialized
/// @param time The current block timestamp cast to a uint32
/// @param upper true for updating a position's upper tick, or false for updating a position's lower tick
/// @param maxLiquidity The maximum liquidity allocation for a single tick
/// @return flipped Whether the tick was flipped from initialized to uninitialized, or vice versa
function update(
    mapping(int24 => Tick.Info) storage self,
    int24 tick,
    int24 tickCurrent,
    int128 liquidityDelta,
    uint256 feeGrowthGlobal0X128,
    uint256 feeGrowthGlobal1X128,
    uint160 secondsPerLiquidityCumulativeX128,
    int56 tickCumulative,
    uint32 time,
    bool upper,
    uint128 maxLiquidity
) internal returns (bool flipped) {
    Tick.Info storage info = self[tick];

    uint128 liquidityGrossBefore = info.liquidityGross;
    uint128 liquidityGrossAfter = LiquidityMath.addDelta(liquidityGrossBefore, liquidityDelta);

    require(liquidityGrossAfter <= maxLiquidity, 'LO');

    flipped = (liquidityGrossAfter == 0) != (liquidityGrossBefore == 0);

    if (liquidityGrossBefore == 0) {
        // by convention, we assume that all growth before a tick was initialized happened _below_ the tick
        if (tick <= tickCurrent) {
            info.feeGrowthOutside0X128 = feeGrowthGlobal0X128;
            info.feeGrowthOutside1X128 = feeGrowthGlobal1X128;
            info.secondsPerLiquidityOutsideX128 = secondsPerLiquidityCumulativeX128;
            info.tickCumulativeOutside = tickCumulative;
            info.secondsOutside = time;
        }
        info.initialized = true;
    }

    info.liquidityGross = liquidityGrossAfter;

    // when the lower (upper) tick is crossed left to right (right to left), liquidity must be added (removed)
    info.liquidityNet = upper
        ? int256(info.liquidityNet).sub(liquidityDelta).toInt128()
        : int256(info.liquidityNet).add(liquidityDelta).toInt128();
}

/// @notice Transitions to next tick as needed by price movement
/// @param self The mapping containing all tick information for initialized ticks
/// @param tick The destination tick of the transition
/// @param feeGrowthGlobal0X128 The all-time global fee growth, per unit of liquidity, in token0
/// @param feeGrowthGlobal1X128 The all-time global fee growth, per unit of liquidity, in token1
/// @param secondsPerLiquidityCumulativeX128 The current seconds per liquidity
/// @param tickCumulative The tick * time elapsed since the pool was first initialized
/// @param time The current block.timestamp
/// @return liquidityNet The amount of liquidity added (subtracted) when tick is crossed from left to right (right to left)
function cross(
    mapping(int24 => Tick.Info) storage self,
    int24 tick,
    uint256 feeGrowthGlobal0X128,
    uint256 feeGrowthGlobal1X128,
    uint160 secondsPerLiquidityCumulativeX128,
    int56 tickCumulative,
    uint32 time
) internal returns (int128 liquidityNet) {
    Tick.Info storage info = self[tick];
    info.feeGrowthOutside0X128 = feeGrowthGlobal0X128 - info.feeGrowthOutside0X128;
    info.feeGrowthOutside1X128 = feeGrowthGlobal1X128 - info.feeGrowthOutside1X128;
    info.secondsPerLiquidityOutsideX128 = secondsPerLiquidityCumulativeX128 - info.secondsPerLiquidityOutsideX128;
    info.tickCumulativeOutside = tickCumulative - info.tickCumulativeOutside;
    info.secondsOutside = time - info.secondsOutside;
    liquidityNet = info.liquidityNet;
}
```

For detailed reasoning, see [white-paper Section 6.3](https://uniswap.org/whitepaper-v3.pdf) for reasoning. A summary from above:

1. Liquidity cap for each tick is: $\text{Max Liquidity} = 2^{128} / \text{number of ticks}$
2. `getFeeGrowthInside(...)` - calculate the fee accrued within current position boundary (specified by `tickLower` and `tickUpper`)
3. `update` - update a tick states, can be any tick (different from current tick). "Outside" variables,
   i.e. `...Outside`, is initialized as:
   $$
   \tag{1}
   \large {
    F^{Outside} =
    \begin{cases}
    F^{Global}, & tick \leq tick_{current} \\
    0, & tick > tick_{current}
    \end{cases}
   }
   $$
4. `cross` - update a tick to next tick as price moves. Notice that `feeGrowthOutside...` and `tickCumulativeOutside` are updated in the same manner:
   $$
   \tag{2}
   \large {
    F^{Outside}_{new} \overset{\Delta}= F^{Global} - F^{Outside}_{old}
   }
   $$

`update` and `cross` rules effectively infer definition for `...Outside` variables, i.e. $F^{Outside}$.
In summary, they keep track of accumulated values either on to the left or right of a specific tick.

# Position

With the tick system defined, we are ready to define a concentrated position of V3. The source code for [`Position.sol`](https://github.com/Uniswap/v3-core/blob/v1.0.0/contracts/libraries/Position.sol)

```solidity
// SPDX-License-Identifier: BUSL-1.1
pragma solidity >=0.5.0 <0.8.0;

// ADDED_NOTE: Visit https://github.com/Uniswap/v3-core/blob/v1.0.0/contracts/libraries/Position.sol
import './FullMath.sol';
import './FixedPoint128.sol';
import './LiquidityMath.sol';

/// @title Position
/// @notice Positions represent an owner address' liquidity between a lower and upper tick boundary
/// @dev Positions store additional state for tracking fees owed to the position
library Position {
  // info stored for each user's position
  struct Info {
    // the amount of liquidity owned by this position
    uint128 liquidity;
    // fee growth per unit of liquidity as of the last update to liquidity or fees owed
    uint256 feeGrowthInside0LastX128;
    uint256 feeGrowthInside1LastX128;
    // the fees owed to the position owner in token0/token1
    uint128 tokensOwed0;
    uint128 tokensOwed1;
  }

  /// @notice Returns the Info struct of a position, given an owner and position boundaries
  /// @param self The mapping containing all user positions
  /// @param owner The address of the position owner
  /// @param tickLower The lower tick boundary of the position
  /// @param tickUpper The upper tick boundary of the position
  /// @return position The position info struct of the given owners' position
  function get(
    mapping(bytes32 => Info) storage self,
    address owner,
    int24 tickLower,
    int24 tickUpper
  ) internal view returns (Position.Info storage position) {
    position = self[keccak256(abi.encodePacked(owner, tickLower, tickUpper))];
  }

  /// @notice Credits accumulated fees to a user's position
  /// @param self The individual position to update
  /// @param liquidityDelta The change in pool liquidity as a result of the position update
  /// @param feeGrowthInside0X128 The all-time fee growth in token0, per unit of liquidity, inside the position's tick boundaries
  /// @param feeGrowthInside1X128 The all-time fee growth in token1, per unit of liquidity, inside the position's tick boundaries
  function update(
    Info storage self,
    int128 liquidityDelta,
    uint256 feeGrowthInside0X128,
    uint256 feeGrowthInside1X128
  ) internal {
    Info memory _self = self;

    uint128 liquidityNext;
    if (liquidityDelta == 0) {
      require(_self.liquidity > 0, 'NP'); // disallow pokes for 0 liquidity positions
      liquidityNext = _self.liquidity;
    } else {
      liquidityNext = LiquidityMath.addDelta(_self.liquidity, liquidityDelta);
    }

    // calculate accumulated fees
    // ADDED NOTE: Q128 = 2^128
    uint128 tokensOwed0 = uint128(
      FullMath.mulDiv(feeGrowthInside0X128 - _self.feeGrowthInside0LastX128, _self.liquidity, FixedPoint128.Q128)
    );
    uint128 tokensOwed1 = uint128(
      FullMath.mulDiv(feeGrowthInside1X128 - _self.feeGrowthInside1LastX128, _self.liquidity, FixedPoint128.Q128)
    );

    // update the position
    if (liquidityDelta != 0) self.liquidity = liquidityNext;
    self.feeGrowthInside0LastX128 = feeGrowthInside0X128;
    self.feeGrowthInside1LastX128 = feeGrowthInside1X128;
    if (tokensOwed0 > 0 || tokensOwed1 > 0) {
      // overflow is acceptable, have to withdraw before you hit type(uint128).max fees
      self.tokensOwed0 += tokensOwed0;
      self.tokensOwed1 += tokensOwed1;
    }
  }
}
```

From the above implementation:

- A position stores total liquidity `L` and total fee accumulated within the concentrated range `[tickLower, tickUpper)`.
- A position is indexed by `owner, tickLower, tickUpper` using [`keccak256`](https://keccak.team/keccak_specs_summary.html) (precede SHA-3) hash.
- As $\Delta X$ and $\Delta Y$ both involves common factor $L$ [from (2) and (3)], it's more efficient to use fee per unit of liquidity
  and derive the total amount once every update, as seen in [pool contract](https://github.com/Uniswap/v3-core/blob/main/contracts/UniswapV3Pool.sol#L379).
  `feeGrowthInside...` is padded with 128 "0" bits to make `mulDiv` works ([mulDiv(a, b, denominator)](https://github.com/Uniswap/v3-core/blob/main/contracts/libraries/FullMath.sol#L14) calculate `a x b / denominator` with full precision).

Since the position is non-fungible in nature, it will be reasonable to represent them as an NFT. As quoted:

<Quote author="Uniswap V3 white-paper">
  "Fees earned in earlier versions were continuously deposited in the pool as liquidity. This meant that liquidity in the pool would grow
  over time, even without explicit deposits, and that fee earnings compounded. In Uniswap v3, due to the non-fungible nature of positions,
  this is no longer possible. Instead, fee earnings are stored separately and held as the tokens in which the fees are paid.

... in v3, the pool contract does not implement the ERC-20 standard. Anyone can create an ERC-20 token contract in the periphery that
makes a liquidity position more fungible, but it will have to have additional logic to handle distribution of,
or reinvestment of, collected fees. Alternatively, anyone could create a periphery contract that wraps an individual liquidity position
(including collected fees) in an ERC-721 non-fungible token."

</Quote>

# Outro

Uniswap V3's tick and position mechanism introduces a new paradigm in liquidity provision, offering liquidity providers unprecedented control and customization.
With the ability to concentrate liquidity within specific price ranges, LPs can optimize their fee earnings.
As we continue our exploration into Uniswap V3 in the upcoming sections, we will unravel the intricacies of the Pool contract,
empowering ourselves with the knowledge to navigate the protocol's inner workings such as **position minting** and **token swaps**.
Stay tuned for part 03 of our journey into the heart of Uniswap V3 - "Minting and Swapping".
